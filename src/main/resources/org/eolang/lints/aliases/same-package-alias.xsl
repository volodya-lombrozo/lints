<?xml version="1.0" encoding="UTF-8"?>
<!--
* SPDX-FileCopyrightText: Copyright (c) 2016-2026 Objectionary.com
* SPDX-License-Identifier: MIT
-->
<xsl:stylesheet xmlns:xsl="http://www.w3.org/1999/XSL/Transform" xmlns:eo="https://www.eolang.org" id="same-package-alias" version="2.0">
  <xsl:import href="/org/eolang/funcs/lineno.xsl"/>
  <xsl:import href="/org/eolang/funcs/escape.xsl"/>
  <xsl:import href="/org/eolang/funcs/defect-context.xsl"/>
  <xsl:output encoding="UTF-8" method="xml"/>
  <xsl:template match="/">
    <defects>
      <xsl:variable name="pkg" select="/object/metas/meta[head='package'][1]/tail"/>
      <xsl:for-each select="/object/metas/meta[head='alias' and count(part)=2]">
        <xsl:variable name="local" select="part[1]"/>
        <xsl:variable name="fqn" select="replace(part[2], '^Φ\.', '')"/>
        <xsl:if test="$pkg != '' and $fqn = concat($pkg, '.', $local)">
          <xsl:element name="defect">
            <xsl:variable name="line" select="eo:lineno(@line)"/>
            <xsl:attribute name="line">
              <xsl:value-of select="$line"/>
            </xsl:attribute>
            <xsl:if test="$line = '0'">
              <xsl:attribute name="context">
                <xsl:value-of select="eo:defect-context(.)"/>
              </xsl:attribute>
            </xsl:if>
            <xsl:attribute name="severity">
              <xsl:text>error</xsl:text>
            </xsl:attribute>
            <xsl:text>The alias </xsl:text>
            <xsl:value-of select="eo:escape($fqn)"/>
            <xsl:text> is redundant, because it refers to an object in the same package (</xsl:text>
            <xsl:value-of select="eo:escape($pkg)"/>
            <xsl:text>) as the current file</xsl:text>
          </xsl:element>
        </xsl:if>
      </xsl:for-each>
    </defects>
  </xsl:template>
</xsl:stylesheet>
